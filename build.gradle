/*
 * Copyright (c) 2019 Connexta, LLC
 *
 * Released under the GNU Lesser General Public License version 3; see
 * https://www.gnu.org/licenses/lgpl-3.0.html
 */
/* Build Script */
import org.gradle.internal.jvm.Jvm

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

plugins {
    id "java"
    id "maven"
    id "com.diffplug.gradle.spotless" version "3.24.2"
    id "com.palantir.docker" version "0.22.1"
    id "org.owasp.dependencycheck" version "5.2.1"
    id "org.springframework.boot" version "2.1.7.RELEASE"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
}

group = "com.connexta.gateway"
version = "0.1.0-SNAPSHOT"

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    implementation {
        exclude group: "org.jdom" // CVE-2019-12814
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE"
    }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-webflux",
            "org.springframework.boot:spring-boot-starter-thymeleaf",
            "org.springframework.boot:spring-boot-starter-security",
            "org.springframework.boot:spring-boot-starter-oauth2-client",
            "org.springframework.boot:spring-boot-starter-actuator",
            "org.springframework.cloud:spring-cloud-starter-gateway",
            "org.springframework.cloud:spring-cloud-starter-security",
            "org.thymeleaf.extras:thymeleaf-extras-springsecurity5",
            "org.springframework.security:spring-security-oauth2-resource-server",
            "org.springframework.security:spring-security-oauth2-jose"
}

sourceCompatibility = 1.11
targetCompatibility = 1.11

if (sourceCompatibility != Jvm.current().javaVersion) {
    throw new Exception("You need Java ${sourceCompatibility} to build and run ${project.name}.\n" +
            "The current version installed is ${Jvm.current().javaVersion.majorVersion}.\n" +
            "For further reading see: \n\t> " +
            "https://github.com/connexta/ion-gateway#prerequisites")
}

spotless {
    File licenseFile = rootProject.file("license.java")
    format "misc", SpotlessConfig.getMisc()
    java SpotlessConfig.getJava(licenseFile)
    groovyGradle SpotlessConfig.getGroovy(licenseFile)
}

dependencyCheck {
    failBuildOnCVSS = 4
    failOnError = true

    analyzers {
        ossIndexEnabled = false
    }

    // Add support for NVD mirror
    if (project.hasProperty("dependencyCheckUrlModified") && project.hasProperty("dependencyCheckUrlBase")) {
        println "Using NVD Mirrors: ${dependencyCheckUrlBase} and ${dependencyCheckUrlModified}"
        cve {
            urlModified = "${dependencyCheckUrlModified}"
            urlBase = "${dependencyCheckUrlBase}"
        }
    }

    suppressionFile = "${projectDir}/owasp-suppressions.xml"
}

configurations.all {
    resolutionStrategy {
        force "com.fasterxml.jackson.core:jackson-databind:[2.9.9.3,)"
        force "com.google.guava:guava:[24.1.1,)"
    }
}

dependencyCheck {
    suppressionFile = "${projectDir}/owasp-suppressions.xml"
}

bootJar {
    launchScript()
}

docker {
    files tasks.bootJar.outputs
    name "cnxta/ion-gateway:${project.version}"
    buildArgs([JAR_FILE: "${tasks.bootJar.outputs.files.singleFile.name}"])
}

springBoot {
    mainClassName = "com.connexta.gateway.GatewayApplication"
}

build.finalizedBy("docker")
bootRun.dependsOn(build)
